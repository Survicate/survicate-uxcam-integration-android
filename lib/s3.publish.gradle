/**
 * Steps to publish:
 * 1. Make sure that you have a valid s3.maven.properties file (it is not committed to git,
 *    use s3.maven.properties.demo as a template)
 * 2. Bump lib_version inside the build.gradle of the lib module.
 * 3. Call ./gradlew :lib:cleanBuildDocPublish in order to make sure it builds correctly (and get rid of
 *    possible errors) and next upload to s3 repo.
 */

tasks.register('androidJavadocsJar', Jar) {
    dependsOn dokkaJavadoc
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

publishing {
    Properties properties = new Properties()
    def propertiesFile = file('s3.maven.properties')
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
    }

    def user = properties.getProperty("maven.user")
    def password = properties.getProperty("maven.password")

    repositories {
        maven {
            name = 'survicateMaven'
            url "s3://repo.survicate.com/"
            credentials(AwsCredentials) {
                accessKey user
                secretKey password
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId 'com.survicate'
            artifactId 'survicate-uxcam-integration'
            version lib_version

            artifact androidJavadocsJar

            pom {
                name = "Survicate UXCam Integration"
                url = "https://github.com/Survicate/survicate-uxcam-integration-android"
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}

// Publish to Survicate S3 maven
tasks.register('cleanBuildDocPublish', GradleBuild) {
    tasks = ['clean', 'assembleRelease', 'publishReleasePublicationToSurvicateMavenRepository']
}

// Publish locally into ~/.m2/repository
task cleanBuildDocPublishLocal(type: GradleBuild) {
    tasks = ['clean', 'assembleRelease', 'publishReleasePublicationToMavenLocal']
}
